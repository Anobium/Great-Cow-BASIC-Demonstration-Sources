''' A demonstration program for GCGB and GCB.
'''
'''
''' Pulse-Width Modulation (PWM) is a scheme that provides power to a load by switching
''' quickly between fully on and fully off states. The PWM signal resembles a square wave
''' where the high portion of the signal is considered the on state and the low portion of
''' the signal is considered the off state. The high portion, also known as the pulse width,
''' can vary in time and is defined in steps. A longer, high on time will illuminate the LED
''' brighter. The frequency or period of the PWM does not change. A larger number of
''' steps applied, which lengthens the pulse width, also supplies more power to the load.
''' Lowering the number of steps applied, which shortens the pulse width, supplies less
''' power. The PWM period is defined as the duration of one complete cycle or the total
''' amount of on and off time combined.
'''
''' This demonstration uses the registers rather than the GCB command set for setting the PWM.
'''
''' This demonstation shows a how the changing PWM values across the attached using the PWM module using timer 2 and PMW1 module.
'''
'''
'''   You need an  LED connnected to porta.0
'''  ************************************************************************************************************************************************************************************
'''
'''  PIC: 10f322
'''  Compiler: GCB
'''  IDE: GCB@SYN
'''
'''
'''@author   EvanV
'''@licence  GPL
'''@version  1.0
'''@date     06.09.2016



    #chip 10f322,16
    #Config MCLRE_ON

    #define PMWOUT1  PORTA.0
    Dir     PMWOUT1 OUT

     ' PWM1POL active_hi, PWM1OE enabled and PWM1EN enabled
     PWM1CON = 0xC0

     'Set PWM hi value
     ' PWM1DCH 15
     PWM1DCH = 0x0F

     'Set PWM lo value
     ' PWM1DCL 3
     PWM1DCL = 0xC0

    ' T2CKPS 1:1, TOUTPS 1:16 and TMR2ON off
    T2CON = 0x78

    ' PR2 31 - PWM pr2 value
    PR2 = 0x1F

    StartTimer 2

    'Show stable signal for 2 s
    wait 2 s

    do forever

        ' Changing PR2 to show Frequency changing
        for PR2 = 1 to 255
            wait 10 ms
        next

        ' Reset PR2
        ' PR2 31
        PR2 = 0x1F
        ' Show stable signal for 2 s
        wait 2 s

        ' Create word variable to address the two bytes to show Width changing
        dim PWM1DC as word Alias PWM1DCH, PWM1DCL
        for PWM1DC = 0 to 0x1fff step 256
            wait 100 ms
        Next
        wait 100 ms

    loop

