'''A demonstration program for GCGB and GCB.
'''--------------------------------------------------------------------------------------------------------------------------------
'''This program searches for I2C devices and display their adresses to the attached PC terminal.
'''This program uses the hardware implementation of I2C and TWI for the Microchip and ATMEL microprocessors.  The microprocessor must have a suitable I2C or TWI module.
'''The hardware serial connections are shown in the program.

'''@author  EvanV
'''@licence GPL
'''@version 1.0a
'''@date    15/12/2015
'''********************************************************************************

; ----- Configuration
  #chip 16F1937, 32
  #option explicit


; ----- Define Hardware settings
  ' Define I2C settings - CHANGE PORTS if required
  #define HI2C_BAUD_RATE 100
  #define HI2C_DATA PORTC.4
  #define HI2C_CLOCK PORTC.3
  'Initialise I2C Slave
  'I2C pins need to be input for SSP module
  Dir HI2C_DATA in
  Dir HI2C_CLOCK in
 'MASTER
  HI2CMode Master

 ' THIS CONFIG OF THE SERIAL PORT WORKS WITH A  MAX232 (or equiv) THEN TO PC
 ' USART settings - CHANGE PORTS if required
 #define USART_BAUD_RATE 9600
 Dir PORTc.6 Out
 Dir PORTc.7 In
 #define USART_TX_BLOCKING
 wait 500 ms

 Dim DISPLAYNEWLINE as Byte


; ----- Main body of program commences here.
  ' Now assumes Serial Terminal is operational
  dim DeviceID as byte

  HSerPrintCRLF
  HSerPrint "Hardware I2C "
  HSerPrintCRLF 2

  HSerPrint "     "
  for DeviceID = 0 to 15
    HSerPrint hex(deviceID)
    HSerPrint " "
  next
  for DeviceID = 0 to 255
    DisplayNewLine = DeviceID % 16
    if DisplayNewLine = 0 Then
      HSerPrintCRLF
      HserPrint hex(DeviceID)
      HSerPrint ": "
    end if
    HSerPrint " "
    HI2CStart
    if HI2CWaitMSSPTimeout <> True then

      HI2CSend ( deviceID )

      if HI2CAckPollState = false then
        HSerPrint   hex(deviceID)
      Else
        HSerPrint "--"
      end if
      HI2CSend ( 0)
      HI2CSend ( 0)

    end if
    HI2CStop

  next

  HSerPrintCRLF 2
  HSerPrint   "End of Search"
  HSerPrintCRLF 2
  wait 1 s
  END



; ----- Support methods.  Subroutines and Functions
