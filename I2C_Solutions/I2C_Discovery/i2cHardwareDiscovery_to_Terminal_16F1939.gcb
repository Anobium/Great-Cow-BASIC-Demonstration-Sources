'''A demonstration program for GCGB and GCB.
'''--------------------------------------------------------------------------------------------------------------------------------
'''This program searches for I2C devices and display their adresses to the attached PC terminal.
'''This program uses the hardware implementation of I2C and TWI for the Microchip and ATMEL microprocessors.  The microprocessor must have a suitable I2C or TWI module.
'''The hardware serial connections are shown in the program.
'''This program requires "DisplayDevices.h" in the same folder.
'''@author  EvanV
'''@licence GPL
'''@version 1.0a
'''@date    10/10/2015
'''********************************************************************************

; ----- Configuration
  #chip 16F1939, 32
  #option explicit
  #config Osc = intOSC, MCLRE_ON, PLLEN_ON
  #include "displaydevices.h"


; ----- Define Hardware settings
  ' Define I2C settings - CHANGE PORTS if required
  #define HI2C_BAUD_RATE 100
  #define HI2C_DATA PORTC.4
  #define HI2C_CLOCK PORTC.3
  'Initialise I2C Slave
  'I2C pins need to be input for SSP module
  Dir HI2C_DATA in
  Dir HI2C_CLOCK in
 'MASTER
  HI2CMode Master

 ' THIS CONFIG OF THE SERIAL PORT WORKS WITH A  MAX232 (or equiv) THEN TO PC
 ' USART settings - CHANGE PORTS if required
 #define USART_BAUD_RATE 9600
 Dir PORTc.6 Out
 Dir PORTc.7 In
 #define USART_DELAY 5 ms
 #define SerSendDelayms 10
 #define USART_BLOCKING
 wait 500 ms

; ----- Variables
  Dim DEVICEID, TESTID as Byte

; ----- Main body of program commences here.
         ' Now assumes Serial Terminal is operational
         HSerPrintCRLF 2
         HSerPrint "Hardware I2C "
         HSerPrint ChipNameStr
         HSerPrintCRLF

              for deviceID = 0 to 255
                HI2CStart
                if HI2CWaitMSSPTimeout <> True then

                    HI2CSend ( deviceID )

                    if HI2CAckPollState = false then
                      HSerPrintCRLF
                      HSerPrint   "ID: 0x"
                      HSerPrint   hex(deviceID)
                      HSerSend 9
                      testid =  deviceID | 1
                      DisplayDevices
                      HI2CSend ( 0 )

                    end if

                   HI2CStop

                end if
              next

              HSerPrintCRLF
              HSerPrint   "End Search"
         HSerPrintCRLF 2

         end


; ----- Support methods.  Subroutines and Functions
