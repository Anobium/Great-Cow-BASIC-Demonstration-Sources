'''A demonstration program for GCGB and GCB.
'''--------------------------------------------------------------------------------------------------------------------------------
'''This program searches for I2C devices and display their adresses to the attached PC terminal.
'''This program uses the hardware implementation of I2C for the Microchip microprocessors.
'''The microprocessor must have a suitable I2C module.
'''The hardware serial connections are shown in the program.

'''@author  EvanV
'''@licence GPL
'''@version 1.1a
'''@date    10/2/2017
'''********************************************************************************

; ----- Configuration
  #chip 18f14k22, 64
  #option explicit

  ' Define I2C settings - CHANGE PORTS
  #define HI2C_BAUD_RATE 400
  #define HI2C_DATA PORTB.4
  #define HI2C_CLOCK PORTB.6
  'Initialise I2C Slave
  'I2C pins need to be input for SSP module
  Dir HI2C_DATA in
  Dir HI2C_CLOCK in
  'MASTER
  HI2CMode Master

 ' THIS CONFIG OF THE SERIAL PORT WORKS WITH max232 THEN TO PC
 ' USART settings
  #define USART_BAUD_RATE 9600
  Dir PORTb.7 Out
  #define USART_BLOCKING
  wait 500 ms

  #define SWITCH_DOWN         0
  #define SWITCH_UP           1

  #define SWITCH      PORTA.5

; ----- Main body of program commences here.
  ' Now assumes Serial Terminal is operational
  dim DeviceID as byte
  Dim DISPLAYNEWLINE as Byte

 do
     HSerPrintCRLF 2
     HSerPrint "Hardware I2C "
     HSerPrint ChipNameStr
     HSerPrintCRLF

      ' Now assumes Serial Terminal is operational
      HSerPrintCRLF
      HSerPrint "   "
      'Create a horizontal row of numbers
      for DeviceID = 0 to 15
        HSerPrint hex(deviceID)
        HSerPrint " "
      next

      'Create a vertical column of numbers
      for DeviceID = 0 to 255
        DisplayNewLine = DeviceID % 16
        if DisplayNewLine = 0 Then
          HSerPrintCRLF
          HserPrint hex(DeviceID)
          if DisplayNewLine > 0 then
            HSerPrint " "
          end if
        end if
        HSerPrint " "

        'Do an initial Start
        HI2CStart
        if HI2CWaitMSSPTimeout <> True then

          'Send to address to device
          HI2CSend ( deviceID )

          'Did device fail to respond?
          if HI2CAckPollState = false then
            HI2CSend ( 0 )
            HSerPrint   hex(deviceID)
          Else
            HSerPrint "--"
          end if

          'Do a stop.
          HI2CStop
        Else
          'Some thing is not correct.... Clock or Data line disconnect?
          HSerPrint "! "
        end if

      next

      HSerPrintCRLF 2
      HSerPrint   "End of Search"
      HSerPrintCRLF 2
      wait 1 s
      wait while switch = On
  loop

; ----- Support methods.  Subroutines and Functions
