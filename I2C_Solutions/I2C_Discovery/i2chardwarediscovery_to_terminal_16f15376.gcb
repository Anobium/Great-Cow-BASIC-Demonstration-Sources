'''
'''******************************************************************
''' Serial connected to B.6 AT 19200 bps to a PC terminal
''' Input switch on A.5 pulled up with 10k
''' I2C SDA on B.5
''' I2C SCL on B.7
'''
'''  PIC: 16f15376
'''  Compiler: GCB
'''  IDE: GCB@SYN
'''
'''  Date: 19.12.2018
'''

'Example discovering PCF8563 Clock
'
'Hardware I2C
'
'
'   00 01 02 03 04 05 06 07 08 09 0A 0B 0C 0D 0E 0F
'00 -- -- -- -- -- -- -- -- -- -- -- -- -- -- -- --
'10 -- -- -- -- -- -- -- -- -- -- -- -- -- -- -- --
'20 -- -- -- -- -- -- -- -- -- -- -- -- -- -- -- --
'30 -- -- -- -- -- -- -- -- -- -- -- -- -- -- -- --
'40 -- -- -- -- -- -- -- -- -- -- -- -- -- -- -- --
'50 -- -- -- -- -- -- -- -- -- -- -- -- -- -- -- --
'60 -- -- -- -- -- -- -- -- -- -- -- -- -- -- -- --
'70 -- -- -- -- -- -- -- -- -- -- -- -- -- -- -- --
'80 -- -- -- -- -- -- -- -- -- -- -- -- -- -- -- --
'90 -- -- -- -- -- -- -- -- -- -- -- -- -- -- -- --
'A0 -- -- A2 A3 -- -- -- -- -- -- -- -- -- -- -- --
'B0 -- -- -- -- -- -- -- -- -- -- -- -- -- -- -- --
'C0 -- -- -- -- -- -- -- -- -- -- -- -- -- -- -- --
'D0 -- -- -- -- -- -- -- -- -- -- -- -- -- -- -- --
'E0 -- -- -- -- -- -- -- -- -- -- -- -- -- -- -- --
'F0 -- -- -- -- -- -- -- -- -- -- -- -- -- -- -- --
'
'End of Search


' ----- Configuration
    'Chip Settings.
    #chip 16f15376, 32
    #option explicit


    'Generated by PIC PPS Tool for Great Cow Basic
    'PPS Tool version: 0.0.5.22
    'PinManager data: Not available (3)
    'Generated for 16F15376
    '
    'Template comment at the start of the config file
    '
    #startup InitPPS, 85
    #define PPSToolPart 16F15376

    Sub InitPPS

            'Module: MSSP1
            RB5PPS = 0x0016    'SDA1 > RB5
            SSP1DATPPS = 0x000D    'RB5 > SDA1 (bi-directional)
            RB7PPS = 0x0015    'SCL1 > RB7
            SSP1CLKPPS = 0x000F    'RB7 > SCL1 (bi-directional)
            'Module: EUSART1
            RB6PPS = 0x000F    'TX1 > RB6
    End Sub
    'Template comment at the end of the config file



    #define SWITCH_DOWN         0
    #define SWITCH_UP           1

    #define SWITCH      PORTA.5

    #define USART_BAUD_RATE 19200
    #define USART_TX_BLOCKING
    #define sync SYNC_TX1STA

; ----- Define Hardware settings for hwi2c
    ' Define I2C settings - CHANGE PORTS if required for your specific device.
    #define hi2c_BAUD_RATE 400
    #define hi2c_DATA PORTb.5
    #define hi2c_CLOCK PORTb.7
    'Initialise I2C Master
    'I2C pins need to be input for SSP2 module
    Dir hi2c_DATA in
    Dir hi2c_CLOCK in

    hi2cMode Master

; ----- Main body of program commences here.
  ' Now assumes Serial Terminal is operational
  dim DeviceID as byte
  Dim DISPLAYNEWLINE as Byte

 do
    HSerPrintCRLF
    HSerPrint "Hardware I2C "
    HSerPrintCRLF 2

      ' Now assumes Serial Terminal is operational
      HSerPrintCRLF
      HSerPrint "   "
      'Create a horizontal row of numbers
      for DeviceID = 0 to 15
        HSerPrint hex(deviceID)
        HSerPrint " "
      next

      'Create a vertical column of numbers
      for DeviceID = 0 to 255
        DisplayNewLine = DeviceID % 16
        if DisplayNewLine = 0 Then
          HSerPrintCRLF
          HserPrint hex(DeviceID)
          if DisplayNewLine > 0 then
            HSerPrint " "
          end if
        end if
        HSerPrint " "

        'Do an initial Start
        HI2CStart
        if HI2CWaitMSSPTimeout <> True then

          'Send to address to device
          HI2CSend ( deviceID )

          'Did device fail to respond?
          if HI2CAckPollState = false then
            HI2CSend ( 0 )
            HSerPrint   hex(deviceID)
          Else
            HSerPrint "--"
          end if

          'Do a stop.
          HI2CStop
        Else
          HSerPrint "! "
        end if

      next

      HSerPrintCRLF 2
      HSerPrint   "End of Search"
      HSerPrintCRLF 2
      wait 1 s
      wait while switch = On
  loop

; ----- Support methods.  Subroutines and Functions
