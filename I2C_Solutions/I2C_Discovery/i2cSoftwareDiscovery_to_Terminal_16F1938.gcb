'''A demonstration program for GCGB and GCB.
'''--------------------------------------------------------------------------------------------------------------------------------
'''This program searches for I2C devices and display their adresses to the attached PC terminal.
'''This program uses the software implementation of I2C for the Microchip microprocessors.
'''The hardware serial connections are shown in the program.
'''This program requires "DisplayDevices.h" in the same folder.
'''@author  EvanV
'''@licence GPL
'''@version 1.0a
'''@date    14.02.15
'''********************************************************************************

; ----- Configuration
  #chip 16F1938, 32
  #option explicit
  #config Osc = intOSC, MCLRE_ON, PLLEN_ON
  #include "displaydevices.h"


; ----- Define Hardware settings
  ' Define I2C settings - CHANGE PORTS
 #define I2C_MODE Master
 #define I2C_DATA PORTC.4
 #define I2C_CLOCK PORTC.3
 #define I2C_DISABLE_INTERRUPTS ON

 ' THIS CONFIG OF THE SERIAL PORT WORKS WITH A  MAX232 (or equiv) THEN TO PC
 ' USART settings - CHANGE PORTS if required
 #define USART_BAUD_RATE 9600
 Dir PORTc.6 Out
 Dir PORTc.7 In
 #define USART_DELAY 5 ms
 #define SerSendDelayms 10
 #define USART_BLOCKING
 wait 500 ms

;----- Variables
  Dim TESTID as Byte

; ----- Main body of program commences here.
         ' Now assumes Serial Terminal is operational
         HSerPrintCRLF
         HSerPrint "Software I2C "
         HSerPrintCRLF 2

   wait 2 s
  dim DeviceID as byte

      for DeviceID = 0 to 255
        I2CStart
        I2CSend ( deviceID )
        I2CSend ( 0 )
        i2cstop

        if I2CSendState = True  then
          HSerPrint   "__"
          HSerPrint   "ID: 0x"
          HSerPrint   hex(deviceID)
          HSerPrint   " (d"
          HSerPrint   Str(deviceID)
          HSerPrint   ") - "
          HSerSend  9
          HSerPrint   "I2C Port "
          testid = deviceID | 1
          DisplayDevices
          HSerPrintCRLF
        end if
      next
      HSerPrint   "End of Search": HSerPrintCRLF 2
  END
