'''
'''  This demonstration show the transmission and receipt of SPI data.
'''  The ADC port value from the Master is sent to the Slave, both show the value (send and received) on the serial terminal.
'''
'''  This demonstration has two parts the Master and the Slave.  Use 16F18855_SPI_OutDemo.gcb as the Master and 16F18855_SPI_InDemo.gcb for the Slave
'''
'''
''' What do you need?  This used two Xpress Boards and four connections between the boards.
'''     Connections.  0v on board board
'''                   Master MOSI to Slave MISO
'''                   Master MISO to Slave MOSI
'''                   Master Clock to Slave Clock
'''
''' The Master reads the ADV value and send to Slave via SPI
''' The Slave reads the SPI incoming value and shows on the terminal
'''
'''
''' This program can be improved to use a control select line (CS) to tell the Slave data is coming, and,
''' this program can be improvied to use an Interrupt to detect incoming SPI data.  I will let you experiment.
'''
'''
'''  PIC: 16F18855
'''  Compiler: GCB
'''  IDE: GCB@SYN
'''
'''  Board: Xpress Evaluation Board
'''  Date: 19.10.2016
'''

' ----- Configuration
    #chip 16f18855
    #option explicit


    'Set the PPS of the PWM and the RS232 ports.
    'Set the PPS for this solution.
  #startup InitPPS, 85

  Sub InitPPS
    UNLOCKPPS
      RC0PPS = 0x0010     'RC0->EUSART:TX;
      RXPPS  = 0x0011     'RC1->EUSART:RX;

      'Module: MSSP1
      RB3PPS = 0x0014    'SCL1 > RB3
      SSP1CLKPPS = 0x000B    'RB3 > SCL1 (bi-directional)
      RB4PPS = 0x0015    'SDA1 > RB4
      SSP1DATPPS = 0x000C    'RB4 > SDA1 (bi-directional)
      RB5PPS = 0x0015    'SDO1 > RB5


    LOCKPPS
  End sub

    #define USART_BAUD_RATE 19200
    #define USART_TX_BLOCKING

    #define SWITCH_DOWN         0
    #define SWITCH_UP           1

    #define LEDD1       porta.0
    #define SWITCH      PORTA.5

    'Potentitor is connected to RA4...set as input
    dir porta.4 in
    'setup switch (SW1)
    dir SWITCH in

    'Set SPI Mode to master, with fast clock
    SPIMode MasterFast
    'setup direction of SPI pins
    dir PORTB.3 out    'clock
    dir PORTB.4 in     'data in
    dir PORTB.5 out    'data out


    dir LEDD1 out


    HSerPrintCRLF 2
    HSerPrint "Great Cow Basic @2016": HSerPrintCRLF
    HSerPrint "SPI Board": HSerPrintCRLF 2
    wait 1 s

    dim myValue, InValue as byte

    do

      'Show BYTE read of ADC just passing the Analog address
      myValue = ReadAD ( AN4  )
      HSerPrint "AN4: "
      hserprint myValue
      HSerPrintCRLF

      SPITransfer myValue, InValue
      wait 100 ms
      LEDD1 = not LEDD1
    loop




