'  FILE:    PIC_GCB_VFD_Example
'  DATE:    12/05/2020
'  VERSION: 1.0
'  AUTHOR:  Anobium
'
'This program was tested using the NORITAKE ITRON VFD GU140x16G-7806A
'
'THIS SOFTWARE IS PROVIDED "AS IS". HOBBY COMPONENTS MAKES NO WARRANTIES,
'WHETHER EXPRESS, IMPLIED OR STATUTORY, INCLUDING, BUT NOT LIMITED TO, IMPLIED
'WARRANTIES OF MERCHANTABILITY AND FITNESS FOR A PARTICULAR PURPOSE, ACCURACY OR
'LACK OF NEGLIGENCE. HOBBY COMPONENTS SHALL NOT, IN ANY CIRCUMSTANCES, BE LIABLE
'FOR ANY DAMAGES INCLUDING, BUT NOT LIMITED TO, SPECIAL, INCIDENTAL OR
'CONSEQUENTIAL DAMAGES FOR ANY REASON WHATSOEVER.


#chip 18F47K42
#config mclr=on



'Use LCD in 4 pin mode and define LCD pins
#DEFINE LCD_IO 4
#DEFINE LCD_VFD_DELAY 250 us

#DEFINE LCD_RW PORTE.1
#DEFINE LCD_RS PORTE.0
#DEFINE LCD_Enable PORTE.2

#DEFINE LCD_DB4 PORTD.4
#DEFINE LCD_DB5 PORTD.5
#DEFINE LCD_DB6 PORTD.6
#DEFINE LCD_DB7 PORTD.7


'A load of VFD constants

#define VFDFONT5x5            0x41   'A
#define VFDFONT5x7            0x42   'B
#define VFDFONT10x14          0x43   'C

#define VFDFONTLarge          0x4C   'L
#define VFDFONTInternational  0x49   'I
#define VFDFONTI              0x49   'I

#define VFDFONT5x7E    0x62   'b
#define VFDFONT10x14E  0x63   'c

#define VFDFONTPIXEL1 0x31   '1
#define VFDFONTPIXEL2 0x32   '2

#define  VFDInvertArea  0x49
#define  VFDFillArea    0x46
#define  VFDClearArea   0x43
#define  VFDOutlineBox  0x4F
#define  VFDClearBox    0x6F



  Print "Great Cow BASIC 2020"
  wait 7 s
  ClS
  VFDFontSelect (VFDFONT5x5  )
  Print "Great Cow BASIC 2020"
  wait 2 s
  ClS


  'Main program
  do
      Repeat 4
          'Send Font Change
          VFDFontSelect ( VFDFONT5x7  )
          VFDFontSelect ( VFDFONTPIXEL1  )
          VFDLOCATE ( 0x0D, 0x0B )
          Print "SETUP"
          VFDLOCATE ( 0x36, 0x0B )
          Print "PRINT"
          VFDLOCATE ( 0x65, 0x0B )
          Print "RUN"

          VFDFill ( 0x0A, 0x02, 0x2C, 0x0C,  VFDOutlineBox )
          VFDFill ( 0x33, 0x02, 0x55, 0x0C,  VFDOutlineBox )
          VFDFill ( 0x5C, 0x02, 0x7E, 0x0C,  VFDOutlineBox )

          VFDFill ( 0x0B, 0x0d, 0x2D, 0x0D,  VFDFillArea )
          VFDFill ( 0x2D, 0x03, 0x2D, 0x0D,  VFDFillArea )

          VFDFill ( 0x34, 0x0d, 0x56, 0x0D,  VFDFillArea )
          VFDFill ( 0x56, 0x03, 0x56, 0x0D,  VFDFillArea )

          VFDFill ( 0x5D, 0x0d, 0x7F, 0x0D,  VFDFillArea )
          VFDFill ( 0x7F, 0x03, 0x7F, 0x0D,  VFDFillArea )

          WAIT 500 ms
          VFDFill ( 0x5D, 0x03, 0x7D, 0x0B,  VFDInvertArea )

          WAIT 500 ms
          VFDFill ( 0x5D, 0x03, 0x7D, 0x0B,  VFDInvertArea )

      end Repeat
      CLS
      Print "Great Cow BASIC 2020"
      wait 1 s
      ClS

  loop

Sub VFDFill (In VFDX1, in VFDY1, in VFDX2, VFDY2, in VFDCmd )

      SET LCD_RS OFF
      LCDWriteByte ( 0xF1 )
      wait 250 us
      SET LCD_RS ON
      wait 250 us
      LCDWriteByte ( VFDX1 )
      LCDWriteByte ( VFDY1 )
      LCDWriteByte ( VFDX2 )
      LCDWriteByte ( VFDY2 )
      LCDWriteByte ( VFDCmd )

End Sub



Sub VFDLocate (In LCDLine, In LCDColumn)

      SET LCD_RS OFF
      LCDWriteByte ( 0xF0 )
      wait 250 us
      SET LCD_RS ON
      wait 250 us
      LCDWriteByte ( LCDLine )
      LCDWriteByte ( LCDColumn )


End Sub







Sub VFDFontSelect ( in VFDFontSelected )

/*
  Select font type, font size and font spacing.
  Font:- ‘A’ or ‘a’ = proportional 64 character mini-font.
  ‘B’ = 5x7 LCD compatible font with Katakana characters.
  ‘C’ = 10x14 LCD compatible font with Katakana characters.

  ‘L’ = LCD Font (default)*
  ‘I’ = International Font*

  ‘b’ = 5x7 international font with European characters.
  ‘c’ = 10x14 international font with European characters.
  ‘1’ = set the inter-character pixel spacing to 1 pixel.
  ‘2’ = set the inter-character pixel spacing to 2 pixels.
*/

      SET LCD_RS OFF
      LCDWriteByte ( 0xF2 )
      wait 40 us
      SET LCD_RS ON
      LCDWriteByte ( VFDFontSelected )
      SET LCD_RS ON

End Sub

Table Graphics1
0xF1, 0x30, 0x04, 0x37, 0x0B, 0x68, 0x1C, 0x5C, 0x48, 0x3E, 0x1D, 0x1D, 0x14, 0x36
End Table

Table Graphics2

0xF1, 0x50, 0x00, 0x63, 0x0F, 0x76, 0x00, 0x00, 0x00, 0x00, 0x07, 0x04, 0xC7, 0xFE, 0x72, 0x73
0x32, 0x3E, 0x3F, 0x1D, 0x00, 0x00, 0x00, 0x00, 0x00, 0x00
0x00, 0x3C, 0x42, 0x81, 0xB9, 0xC1, 0x42, 0x7C, 0x20, 0xD8
0xFC, 0x3C, 0xFC, 0xCA, 0x49, 0xB1, 0x89, 0x42, 0x3C, 0x00
End Table
