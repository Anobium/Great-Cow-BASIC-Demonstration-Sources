'''A demonstration program for GCGB and GCB.
'''--------------------------------------------------------------------------------------------------------------------------------
'''This program will count from 0 to 9999 on four 7 segment LED display.
''':
'''This uses 7 segment port (all 8 bits) addressing and a specific command to select the specific 7 segment display.
''':
'''The 7 Segment displays are connected to PortB.0-6 for segments A-G respectively.
'''The 7 Segment displays enable pins are connected to PortA.0, A,1, A.2 and A.3.
'''@author  EvanV plus works of HughC
'''@licence GPL
'''@version 2.0a
'''@date    02.10.2016
'''modified 10/28/2016 mlo
'''********************************************************************************

' ----- Configuration

#CHIP 16F877A, 4

' ----- Define Hardware settings
' No hardware specifics setting for 7-Segment display shown below using the constants
' You MUST specify constants as show below in the constants section.

' ----- Constants
' You NEED to specify the port settings

'Set the 7 segment port for the first 7-Segment display
#DEFINE DisplayPortA PORTB
'Set the 7 segment as the same port as the second display
#DEFINE DisplayPortB PORTB
'Set the 7 segment as the same port as the third display
#DEFINE DisplayPortC PORTB
'Set the 7 segment as the same port as the forth display
#DEFINE DisplayPortD PORTB

'The select pin for the first 7-Segment display
#DEFINE DispSelectA Set portA.0 On
'The select pin for the second  7-Segment display
#DEFINE DispSelectB Set portA.1 On
'The select pin for the third 7-Segment display
#DEFINE DispSelectC Set portA.2 On
'The select pin for the forth 7-Segment display
#DEFINE DispSelectD Set portA.3 On
'The select pin for the first 7-Segment display
#DEFINE DispDeSelectA Set portA.0 Off
'The select pin for the second  7-Segment display
#DEFINE DispDeSelectB Set portA.1 Off
'The select pin for the third 7-Segment display
#DEFINE DispDeSelectC Set portA.2 Off
'The select pin for the firstforth 7-Segment display
#DEFINE DispDeSelectD Set portA.3 Off

' ----- Variables
'Words are needed to support the numbers beyond the byte value of 255
'Note: SysCalcTempX is the carry left over from divisions. We use this!!
Dim Counter, DCarry, DData, SysCalcTempX As Word

' ----- Main body of program commences here.

Do Forever
    For Counter = 0 To 10000

        'Show the value on the 7-Segment displays
        'Calculate 1000
        THOU = Counter / 1000
        DCarry = SysCalcTempX
'

        'Calculate 100's
        HUND = DCarry / 100
        DCarry = SysCalcTempX
'
        'Calculate 10's
        TENS = DCarry / 10
        ONES = SysCalcTempX

'
        Repeat 125
            DisplayValue 1, THOU
            Wait 2 ms
            DisplayValue 2, HUND
            Wait 2 ms
            DisplayValue 3, TENS
            Wait 2 ms
            DisplayValue 4, ONES
            Wait 2 ms
        End Repeat


        Wait 5 ms

    Next

Loop
