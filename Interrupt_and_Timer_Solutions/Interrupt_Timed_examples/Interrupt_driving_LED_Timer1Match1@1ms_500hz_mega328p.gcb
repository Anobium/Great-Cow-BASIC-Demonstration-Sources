'''A program  for GCGB and GCB.
'''--------------------------------------------------------------------------------------------------------------------------------
'''    Description. Interrupt driving LED using Timer1 match.
'''    This calls the Timer1Match1 every 1ms.
'''     You can use the #define SimulatePWM to either control an LED with PWM or Toggle the LED
'''    The SimulatePWM adjusts the power to the LED over 2.5 seconds, then to zero power again over 2.5 seconds with a hold of 1 second between each ramp up/down.
'''    The LED is connected to port via appropiate resistors.
''':
'''
'''
'''

'''@author  EvanV
'''@licence GPL
'''@version 1.0a
'''@date    31.03.2017

' ----- Configuration
  #chip mega328p,16
  #option explicit

'  #define SimulatePWM 'comment this to just toggle the LED

  'hardware
  #define LED PORTB.5
  dir LED out

  'Call the initialisation routine
  InitLEDControl

  ;----- Variables
    Dim LEDSPEED, PWMCOUNTER, PWMCOUNTER as Byte


'Main routine
  Do

      #ifdef SimulatePWM
          ;Increase LED to full over 2.5 seconds
          For LEDSpeed = 0 to 100
              Wait 25 ms
          Next
          ;Hold LEDPower
          Wait 1 s
          ;Decrease LED to zero over 2.5 seconds
          For LEDSpeed = 100 to 0 step - 1
              Wait 25 ms
          Next
          ;Hold LEDPower
          Wait 1 s
      #endif

  Loop
  end



'Setup routine
Sub InitLEDControl

    set LED off

    'Clear variables
    LEDSpeed = 0
    PWMCounter = 0

    ' Add the handler for the interrupt
    On Interrupt Timer1Match1 Call InterruptHandler
    ' Initialise the timer - this is required
    ' Set prescaler and then start the timer
    InitTimer1 Osc, PS_1_1
    ' Start the timer - this is required
    StartTimer 1
    ' Set the correct regsisters.  These registers give us the corrent timed Interrupt.
    ' Use the TimerCalculator from MikroElectronika to obtain the correct values for this four registers.
    ' When using the TimerCalculator from MikroElectronika paste the four registers ignoring the *_Bit registers

    'Timer1 Prescaler = 0; Preload = 15999; Actual Interrupt Time = 1 ms
    TCCR1A = 0x80
    TCCR1B = 0x09
    OCR1AH = 0x3E
    OCR1AL = 0x7F

End Sub


'This will be called when the Timer matches
Sub InterruptHandler

  #ifdef SimulatePWM

    If LEDSpeed > PWMCounter Then
        Set LED On
    Else
        Set LED Off
    End If
    PWMCounter += 1
    If PWMCounter = 100 Then PWMCounter = 0
  #endif

  #ifndef SimulatePWM
    LED = !LED
  #endif

End Sub
