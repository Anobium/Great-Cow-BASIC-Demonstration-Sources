'''A demonstration program for GCGB and GCB.
'''--------------------------------------------------------------------------------------------------------------------------------
'''This program is a simple GLCD demonstration of the ILI9341 GLCD capabilities.
'''It is a nice graphical LCD, suitable for a lot of various projects.
'''This program draws lines, boxes, circles and prints strings and numbers.
'''The GLCD is connected to the microprocessor as shown in the hardware section of this code.
''':
'''This has been tested using the hardware SPI option.
''':
'''Note: for the correct operation of this display you MUST connect via a CD4050, see http://forum.arduino.cc/index.php?topic=181679.90
''':
'''@author
'''@licence GPL
'''@version 1.1
'''@date
'''********************************************************************************

'Chip Settings.
#CHIP mega328p, 16
#OPTION Explicit

#INCLUDE <glcd.h>
#INCLUDE <uno_mega328p.h >

#DEFINE GLCD_TYPE GLCD_TYPE_ILI9341

'Pin mappings for SPI - this GLCD driver supports Hardware SPI and Software SPI
' Data command line
#DEFINE GLCD_DC       DIGITAL_8
' Chip select line
#DEFINE GLCD_CS       DIGITAL_10
' Reset line
#DEFINE GLCD_RESET          DIGITAL_9

' Data in | MISO    - Not used therefore not really required
#DEFINE GLCD_DI       DIGITAL_12
' Data out | MOSI
#DEFINE GLCD_DO       DIGITAL_11
' Clock Line
#DEFINE GLCD_SCK      DIGITAL_13

' remove/comment out if you want to use software SPI.
#DEFINE ILI9341_HardwareSPI


'GLCD selected extension font set. ASCII characters 31-254, the extended font uses 1358 bytes of program memory
#DEFINE GLCD_EXTENDEDFONTSET1
GLCDfntDefaultSize = 1

'GLCDCLS  supports GLCDBackground as default
'GLCDCLS  also support passing color parameter.
'GLCDCLS [color]
' optionally you can rotate the screen.
GLCDRotate ( Landscape_Rev )


'Available colors
'ILI9341_BLACK
'ILI9341_RED
'ILI9341_GREEN
'ILI9341_BLUE
'ILI9341_WHITE
'ILI9341_PURPLE
'ILI9341_YELLOW
'ILI9341_CYAN
'ILI9341_D_GRAY
'ILI9341_L_GRAY
'ILI9341_SILVER
'ILI9341_MAROON
'ILI9341_OLIVE
'ILI9341_LIME
'ILI9341_AQUA
'ILI9341_TEAL
'ILI9341_NAVY
'ILI9341_FUCHSIA

Dim CCOUNT, BYTENUMBER, OLDFONT, OLDFONT As Byte

CCount = 31
Dim longNumber As Long
' max value = 4294967290
longNumber = 0
Dim wordNumber As Word
wordNumber = 0
byteNumber = 0


Repeat 2

    GLCDRotate ( Landscape_Rev )
    GLCDCLS ILI9341_BLUE

    GLCDForeground = ILI9341_MAROON
    GLCDfntDefaultSize = 1
    GLCDPrint ( 2, 2, "GLCDPrint String" )


    GLCDForeground = ILI9341_WHITE
    GLCDfntDefaultSize = 2
    GLCDPrint ( 2, 12, "GLCDPrint String" )


    GLCDForeground = ILI9341_YELLOW
    GLCDfntDefaultSize = 3
    GLCDPrint ( 2,30, "GLCDPrint String" )

    longNumber = 0xFFFFFFFF
    wordNumber = 0xFFFF
    byteNumber = 0xFF

    GLCDRotate ( Portrait )
    GLCDCLS ILI9341_WHITE

    GLCDForeground = ILI9341_MAROON
    GLCDfntDefaultSize = 1
    GLCDPrint ( 2, 2, bytenumber )


    GLCDForeground = ILI9341_BLACK
    GLCDfntDefaultSize = 2
    GLCDPrint ( 2, 12, wordNumber )


    GLCDForeground = ILI9341_CYAN
    GLCDfntDefaultSize = 3
    GLCDPrint ( 2,30, longnumber )

    Wait 2 s

End Repeat



GLCDCLS ILI9341_BLUE
'Set font size.
GLCDfntDefaultSize = 2
' max value = 4294967290
longNumber = 0
wordNumber = 0
byteNumber = 0

'Using the global variable GLCDfntDefaultsize to show the different capabilities
Select Case GLCDfntDefaultSize

Case 1

    ShowCase1

Case 2

    ShowCase2

Case 3

    GLCDDrawString (2, 1, "GLCD font")
    GLCDDrawString (2, 25, "size = 3")
    Box 0,0,ILI9341_GLCD_WIDTH-1, ILI9341_GLCD_HEIGHT-1, ILI9341_BLUE
    Box ILI9341_GLCD_WIDTH-5, ILI9341_GLCD_HEIGHT-5,ILI9341_GLCD_WIDTH-1, ILI9341_GLCD_HEIGHT-1, ILI9341_WHITE

End Select

End


Sub ShowCase1

    GLCDRotate ( Portrait )

    GLCDDrawString (2, 1, "GCB", ILI9341_WHITE )
    GLCDDrawString (56,1,"v.9804", ILI9341_YELLOW )
    GLCDDrawString (2, 16, "DrawStr", ILI9341_WHITE )

    GLCDDrawString ( 60, 31, "Long Number" )
    GLCDDrawString ( 60, 39, "Word Number" )
    GLCDDrawString ( 60, 47, "Byte Number" )

    GLCDDrawString ( 74, 16, "Asc:" )

    Box 0,0,ILI9341_GLCD_WIDTH-1, ILI9341_GLCD_HEIGHT-1, ILI9341_YELLOW
    Box ILI9341_GLCD_WIDTH-5, ILI9341_GLCD_HEIGHT-5,ILI9341_GLCD_WIDTH-1, ILI9341_GLCD_HEIGHT-1, ILI9341_WHITE

    'center
    Circle( ILI9341_GLCD_WIDTH/2, ILI9341_GLCD_HEIGHT/2, 50, ILI9341_SILVER )
    FilledCircle( ILI9341_GLCD_WIDTH/2, ILI9341_GLCD_HEIGHT/2, 25, ILI9341_D_GRAY  )

    Line 0,  ILI9341_GLCD_HEIGHT-1 , ILI9341_GLCD_WIDTH/2 +100,  (ILI9341_GLCD_HEIGHT /2) - 50, ILI9341_CYAN
    Line  0, (ILI9341_GLCD_HEIGHT /2) - 50, ILI9341_GLCD_WIDTH/2 +100, (ILI9341_GLCD_HEIGHT /2) - 50, ILI9341_CYAN

    FilledBox 2,ILI9341_GLCD_HEIGHT/2+10,42,ILI9341_GLCD_HEIGHT/2+50, ILI9341_RED

    oldfont = GLCDfntDefaultSize
    GLCDfntDefaultSize = 3
    GLCDDrawString ( 42, ILI9341_GLCD_HEIGHT - 30 , "ILI9341 Driver" , ILI9341_WHITE )

    GLCDfntDefaultSize = 1
    GLCDDrawString ( ILI9341_GLCD_WIDTH - 60, 2 , "Dec 2019" , ILI9341_WHITE )
    #IFDEF ILI9341_HardwareSPI
        GLCDDrawString ( ILI9341_GLCD_WIDTH - 42, 10 , "HW SPI" , ILI9341_WHITE )
    #ENDIF

    #IFNDEF ILI9341_HardwareSPI
        GLCDDrawString ( ILI9341_GLCD_WIDTH - 42, 10 , "SW SPI" , ILI9341_WHITE )
    #ENDIF

    'Revert the font size
    GLCDfntDefaultSize = oldfont
    GLCDForeground = ILI9341_WHITE

    Do Forever

        GLCDPrint ( 144 , 31, longNumber)
        GLCDPrint ( 144 , 39, Pad( Str(wordnumber),5))
        GLCDPrint ( 144 , 47, Pad( Str(bytenumber),3))

        GLCDDrawChar(54, 16, CCount )
        'Draw a box around the Char below
        Box 52,15,62,26, ILI9341_WHITE

        GLCDDrawString ( 100, 16 , Pad( Str(CCount),3) )

        CCount++
        If CCount = 0 Then CCount = 31
        If CCount = 128 Then CCount = 192
        longNumber = longNumber + 7

        wordNumber = wordNumber + 3
        byteNumber++

    Loop

End Sub

Sub ShowCase2

    GLCDRotate ( Landscape_Rev )

    GLCDDrawString (2, 1, "GCB",ILI9341_WHITE)
    GLCDDrawString (56,1,"v.941",ILI9341_YELLOW)
    GLCDDrawString (2, 18, "DrawStr",ILI9341_WHITE)

    GLCDDrawString ( 2, 52, "Long:" )
    GLCDDrawString ( 2, 70, "Word:" )
    GLCDDrawString ( 2, 88, "Byte:" )

    GLCDDrawString ( 128, 18, "Asc:"+"  " )

    Box 0,0,ILI9341_GLCD_WIDTH-1, ILI9341_GLCD_HEIGHT-1, ILI9341_YELLOW
    Box ILI9341_GLCD_WIDTH-5, ILI9341_GLCD_HEIGHT-5,ILI9341_GLCD_WIDTH-1, ILI9341_GLCD_HEIGHT-1, ILI9341_WHITE

    'center
    Circle( ILI9341_GLCD_WIDTH/2, ILI9341_GLCD_HEIGHT/2, 50, ILI9341_SILVER )
    FilledCircle( ILI9341_GLCD_WIDTH/2, ILI9341_GLCD_HEIGHT/2, 25, ILI9341_D_GRAY  )

    Line 0,  ILI9341_GLCD_HEIGHT-1 , ILI9341_GLCD_WIDTH/2 +100,  (ILI9341_GLCD_HEIGHT /2) - 50, ILI9341_CYAN
    Line  0, (ILI9341_GLCD_HEIGHT /2) - 50, ILI9341_GLCD_WIDTH/2 +100, (ILI9341_GLCD_HEIGHT /2) - 50, ILI9341_CYAN

    FilledBox 2,ILI9341_GLCD_HEIGHT/2+10,42,ILI9341_GLCD_HEIGHT/2+50, ILI9341_RED

    oldfont = GLCDfntDefaultSize
    GLCDfntDefaultSize = 3
    GLCDDrawString ( 50, ILI9341_GLCD_HEIGHT - 30 , "ILI9341 Driver" , ILI9341_WHITE )

    GLCDfntDefaultSize = 1
    GLCDDrawString ( ILI9341_GLCD_WIDTH - 58, 2 , "Dec 2019" , ILI9341_WHITE )

    #IFDEF ILI9341_HardwareSPI
        GLCDDrawString ( ILI9341_GLCD_WIDTH - 44, 10 , "HW SPI" , ILI9341_WHITE )
    #ENDIF

    #IFNDEF ILI9341_HardwareSPI
        GLCDDrawString ( ILI9341_GLCD_WIDTH - 42, 10 , "SW SPI" , ILI9341_WHITE )
    #ENDIF

    'Revert the font size
    GLCDfntDefaultSize = oldfont
    GLCDForeground = ILI9341_WHITE
    BigPrint_ILI9341 ( 100 , ILI9341_GLCD_HEIGHT - 60 , "Great Cow Basic" )

    Do Forever

        GLCDPrint ( 64 , 52, longNumber)
        GLCDPrint ( 64 , 70, Pad( Str(wordnumber),5))
        GLCDPrint ( 64 , 88, Pad( Str(bytenumber),3))

        GLCDDrawChar(99, 18, CCount )
        'Draw a box around the Char below
        Box 98,18,117,42, ILI9341_WHITE

        GLCDDrawString ( 180, 18 , Pad( Str(CCount),3) )

        CCount++
        If CCount = 0 Then CCount = 31
        If CCount = 128 Then CCount = 192
        longNumber = longNumber + 7
        wordNumber = wordNumber + 3
        byteNumber++

    Loop
End Sub
