'''A demonstration program for GCGB and GCB.
'''The program shows how the I2C2 bus can be used to add a SDD1306 GLCD.
''':
''':
'''@author  EvanV
'''@licence GPL
'''@version 1.0a
'''@date    Jan 2019
'''*************************************************************************

  'Chip Settings.
  #chip 18F27K40,16
  #option explicit


  #include <lowlevel\hwi2c2.h>

    'Generated by PIC PPS Tool for Great Cow Basic
    'PPS Tool version: 0.0.5.25
    'PinManager data: v1.75
    'Generated for 18f27k40
    '
    'Template comment at the start of the config file
    '
    #startup InitPPS, 85
    #define PPSToolPart 18f27k40

    Sub InitPPS

            'Module: EUSART1
            RC6PPS = 0x0009    'TX1 > RC6
            'Module: MSSP2
            RC4PPS = 0x0012    'SDA2 > RC4
            SSP2DATPPS = 0x0014    'RC4 > SDA2 (bi-directional)
            RC3PPS = 0x0011    'SCK2 > RC3
            SSP2CLKPPS = 0x0013    'RC3 > SCK2 (bi-directional)


    End Sub
    'Template comment at the end of the config file


; ----- Define Hardware settings for HWI2C2
  ' Define I2C settings - CHANGE PORTS if required for your specific device.
  #define HI2C2_BAUD_RATE 100
  #define HI2C2_DATA PORTc.4
  #define HI2C2_CLOCK PORTc.3
  'Initialise I2C Master
  'I2C pins need to be input for SSP2 module
  Dir HI2C2_DATA in
  Dir HI2C2_CLOCK in
 'MASTER Second Port
  HI2C2Mode Master
  #include <lowlevel\hwi2c2.h>


  #include <glcd.h>
; ----- Define GLCD Hardware settings
  #define GLCD_TYPE GLCD_TYPE_SSD1306
  #define GLCD_I2C_Address 0x78
; ----- Define variables

  #define GLCD_TYPE_SSD1306_LOWMEMORY_GLCD_MODE
  #define GLCD_TYPE_SSD1306_CHARACTER_MODE_ONLY
  #define GLCD_PROTECTOVERRUN








; ----- Main program

  Dim BYTENUMBER, CCOUNT as Byte

  CCount = 0
  dim longNumber as long
  longNumber = 0 ' max value = 4294967290
  dim wordNumber as Word
  dim outstring as string
  wordNumber = 0
  byteNumber = 0



    #ifdef GLCD_TYPE_SSD1306_LOWMEMORY_GLCD_MODE
      GLCD_Open_PageTransaction 0,7
    #endif

        GLCDPrint 0, 0,   "Great Cow BASIC"
        GLCDPrint (0, 16, "Anobium 2019")
        GLCDPrint (76, 16, GLCD_WIDTH )
        GLCDPrint (96, 16, "*" )
        GLCDPrint (104, 16, GLCD_HEIGHT )

    #ifdef GLCD_TYPE_SSD1306_LOWMEMORY_GLCD_MODE
      GLCD_Close_PageTransaction
    #endif

  wait 3 s
  GLCDCLS

    #ifdef GLCD_TYPE_SSD1306_LOWMEMORY_GLCD_MODE
      GLCD_Open_PageTransaction 0, 7
    #endif

        GLCDPrint 0, 0,   "Version 0.98.04"


        #ifdef GLCD_TYPE_SSD1306_LOWMEMORY_GLCD_MODE
            GLCDPrint (0, 16, "LM GLCD MODE")
        #endif

        #ifndef GLCD_TYPE_SSD1306_LOWMEMORY_GLCD_MODE
            GLCDPrint (0, 16, "FULL GLCD MODE")
        #endif


    #ifdef GLCD_TYPE_SSD1306_LOWMEMORY_GLCD_MODE
      GLCD_Close_PageTransaction
    #endif

  wait 3 s
  GLCDCLS


  DO forever

     for CCount = 32 to 127

          outString = str( CCount )                                  ; Prepare a string


          ' Prepare the static components of the screen
          #ifdef GLCD_TYPE_SSD1306_LOWMEMORY_GLCD_MODE
            GLCD_Open_PageTransaction 0,7
          #endif

            GLCDPrint ( 1,   1, "PrintStr")                                ; Print some text
            GLCDPrint ( 64,  1, "@")                                    ; Print some more text
            GLCDPrint ( 72,  1, ChipMhz)                                   ; Print chip speed
            GLCDPrint ( 86, 1, "Mhz")                                     ; Print some text
            GLCDDrawString( 1,9,"DrawStr")                                 ; Draw some text
            box 0,0,GLCD_WIDTH-1, GLCD_HEIGHT-1                            ; Draw a box
            box GLCD_WIDTH-5, GLCD_HEIGHT-5,GLCD_WIDTH-1, GLCD_HEIGHT-1    ; Draw a box
            Circle( 44,24,5)                                              ; Draw a circle
            line 0,GLCD_HEIGHT-1,44,19                                    ; Draw a line


            GLCDPrint ( 52 ,  18,  "0x" )
            GLCDPrint ( 64 ,  18,  hex(longNumber_E ) )                 ; Print a HEX string
            GLCDPrint ( 76 ,  18,  hex(longNumber_U ) )                 ; Print a HEX string
            GLCDPrint ( 88 ,  18,  hex(longNumber_H ) )                 ; Print a HEX string
            GLCDPrint ( 100 , 18,  hex(longNumber ) )                   ; Print a HEX string


            box (3,24,6,27)                                           ; Draw a Box
            GLCDDrawChar(48, 9, CCount )                               ; Draw a character

            GLCDDrawString(64, 9, pad(outString,3) )                   ; Draw a string

            Ellipse 118, 6, 7, 4
            if CCOUNT < 63 then
              FilledEllipse 118, 6, 5, 2
            end if

        #ifdef GLCD_TYPE_SSD1306_LOWMEMORY_GLCD_MODE
          GLCD_Close_PageTransaction
        #endif

          longNumber = longNumber + 7


      NEXT
  LOOP
  end
