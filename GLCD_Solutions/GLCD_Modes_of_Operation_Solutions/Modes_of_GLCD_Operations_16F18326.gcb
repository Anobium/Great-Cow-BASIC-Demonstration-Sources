'''A program  for GCGB and GCB.
'''--------------------------------------------------------------------------------------------------------------------------------
'''This program demonstrations the options supported by GLCD modes.
'''
'''    #define GLCD_TYPE_SSD1306_CHARACTER_MODE_ONLY
'''    #define GLCD_TYPE_SSD1306_LOWMEMORY_GLCD_MODE

'''  Operational Options
'''
'''  Full GLCD mode - Hi memory device         | No defines required
'''  Low GLCD memory mode - Lo memory device   | GLCD_TYPE_SSD1306_LOWMEMORY_GLCD_MODE ( as GLCD_TYPE_SSD1306_CHARACTER_MODE_ONLY will be set automatically)
'''  Low GLCD memory mode - Hi memory device   | GLCD_TYPE_SSD1306_LOWMEMORY_GLCD_MODE and GLCD_TYPE_SSD1306_CHARACTER_MODE_ONLY to force mode of operation
'''                                              Note: GLCD_TYPE_SSD1306_LOWMEMORY_GLCD_MODE alone WILL NOT operate as expected.  GLCD_TYPE_SSD1306_CHARACTER_MODE_ONLY must be added to user code
'''  Character mode - Lo or Hi memory device   | GLCD_TYPE_SSD1306_CHARACTER_MODE_ONLY

''' Character memory mode - screen addressing
'''     8 lines of text, any GLCDPrint (xpos, YPOS, text) and YPOS will be translated to Address Line #n.
'''     Address Line #0 = YPOS 0 to 7
'''     Address Line #1 = YPOS 8 to 15
'''     Address Line #2 = YPOS 16 to 23
'''     Address Line #3 = YPOS 14 to 31
'''     Address Line #4 = YPOS 32 to 39
'''     Address Line #5 = YPOS 40 to 47
'''     Address Line #6 = YPOS 48 to 55
'''     Address Line #7 = YPOS 56 to 63
'''
'''
'''
''' Low GLCD memory mode
'''
'''   GLCD_Open_PageTransaction and GLCD_Close_PageTransaction added to support Low memory configuration
'''   Any commands between GLCD_Open_PageTransaction and GLCD_Close_PageTransaction should be GLCD focused only.
'''   Any commands within the transaction WILL BE called 8 (EIGHT) times. So, if you increment variables... they will be incremented 8 times
'''   Any commands between GLCD_Open_PageTransaction and GLCD_Close_PageTransaction should be GLCD focused only.
'''   Methods such as scroll, pixelstatus WILL NOT work in low memory mode.
'''   Print characters first then graphics.
'''
'''   Example of low memory usage
'''    #define GLCD_TYPE_SSD1306_CHARACTER_MODE_ONLY    '''This defined will be required on high RAM devices to activate low memory capability
'''    #define GLCD_TYPE_SSD1306_LOWMEMORY_GLCD_MODE
'''
'''      GLCD_Open_PageTransaction 0,7
'''        GLCDPrint 0,10, "Great Cow BASIC"
'''        GLCDPrint 0,52, "Page 2"
'''        Box 0,0,127,63
'''      GLCD_Close_PageTransaction
'''
'''
'''@author     EvanV
'''@licence    GPL
'''@version    1.00
'''@date       21.08.17
'''********************************************************************************

; ----- Configuration
 #chip 16F18326, 32
#Config WRT_OFF, cpd_off, MCLRE_ON    'These configs = User non-votatile protection off (WRT), Data non-votatile protection off (CPD), MCLR pin function enablesd, External oscillator not enabled and clockout function is disables ( enable portc.5 to I/O port).

#include <glcd.h>

    'Generated by PIC PPS Tool for Great Cow Basic
    'PPS Tool version: 0.0.5.9
    'PinManager data: v1.36.1
    '
    'Template comment at the start of the config file
    '
    #startup InitPPS, 85

    Sub InitPPS

            'Module: MSSP1
            RC3PPS = 0x0018    'SCL1 > RC3
            SSP1CLKPPS = 0x0013    'RC3 > SCL1 (bi-directional)
            RC4PPS = 0x0019    'SDA1 > RC4
            SSP1DATPPS = 0x0014    'RC4 > SDA1 (bi-directional)

            'Module: EUSART
            RA0PPS = 0x0014    'TX > RA0
            TXPPS = 0x0000    'RA0 > TX (bi-directional)


    End Sub
    'Template comment at the end of the config file





    'Setup Hardware I2C
    #define HI2C_DATA PORTC.4
    #define HI2C_CLOCK PORTC.3
    'I2C pins need to be input for SSP module when used on Microchip PIC device
    Dir HI2C_DATA in
    Dir HI2C_CLOCK in
    'MASTER MODE
    #define HI2C_BAUD_RATE 400
    HI2CMode Master

    'Define GLCD Hardware settings
    #define GLCD_TYPE GLCD_TYPE_SSD1306
    #define GLCD_I2C_Address 0x78


    '#define GLCD_PROTECTOVERRUN
    #define GLCD_TYPE_SSD1306_CHARACTER_MODE_ONLY
    #define GLCD_TYPE_SSD1306_LOWMEMORY_GLCD_MODE


GLCDUpdateScreen1:

  'Turn LED on to show it is complete - you can put a scopy on this to check the timing
  dir portc.0 out


   Do Forever

    portc.0 = !portc.0

    #ifdef GLCD_TYPE_SSD1306_LOWMEMORY_GLCD_MODE
      GLCD_Open_PageTransaction 0,7
    #endif

        GLCDPrint 0,10, "Great Cow BASIC"

        #ifndef GLCD_TYPE_SSD1306_CHARACTER_MODE_ONLY
          GLCDPrint 0,52, "Full GLCDMode SSD1306"

          Circle 63,31,22
          line 0, 0, 127, 63
          line 0, 63, 127, 0
          Circle 63,31,10
          Box 0,0,127,63
          pset 100,31,1
        #endif


        #ifdef GLCD_TYPE_SSD1306_CHARACTER_MODE_ONLY
          #ifdef GLCD_TYPE_SSD1306_LOWMEMORY_GLCD_MODE
            GLCDPrint 0,52, "LM GLCDMode SSD1306"
          #endif
        #endif

        #ifdef GLCD_TYPE_SSD1306_CHARACTER_MODE_ONLY
          #ifndef GLCD_TYPE_SSD1306_LOWMEMORY_GLCD_MODE
            GLCDPrint 0,52, "TX GLCDMode SSD1306"
          #endif
        #endif

        #ifdef GLCD_TYPE_SSD1306_LOWMEMORY_GLCD_MODE
          pset 100,31,1
          Circle 63,31,22
          line 0, 0, 127, 63
          line 0, 63, 127, 0
          Circle 63,31,10
          Box 0,0,127,63
        #endif

        'try a wait here.... in LM or TX mode you will see the pages appear
        'wait 500 ms

    #ifdef GLCD_TYPE_SSD1306_LOWMEMORY_GLCD_MODE
      GLCD_Close_PageTransaction
    #endif


    wait 3 s
    GLCDCLS

GLCDUpdateScreen2:

    #ifdef GLCD_TYPE_SSD1306_LOWMEMORY_GLCD_MODE
      GLCD_Open_PageTransaction 0,7
    #endif

        GLCDPrint 0,10, "Great Cow BASIC"
        GLCDPrint 0,52, "Page 2"

        #ifndef GLCD_TYPE_SSD1306_CHARACTER_MODE_ONLY
          Box 0,0,127,63
          GLCDPrint 0,20, "Full GLCD mode "
          GLCDPrint 0,30, "needs 1024 RAM"
          GLCDPrint 0,40, "buffer for GLCD"

        #endif

        #ifdef GLCD_TYPE_SSD1306_LOWMEMORY_GLCD_MODE
          Box 0,0,127,63
          GLCDPrint 0,20, "Only a 128 RAM buffer"
          GLCDPrint 0,30, "for GLCD support"
        #endif


    #ifdef GLCD_TYPE_SSD1306_LOWMEMORY_GLCD_MODE
      GLCD_Close_PageTransaction
    #endif

    wait 3 s
    GLCDCLS

GLCDUpdateScreen3:

    dim xrange as byte

    for xrange = 1 to 126 step 4
        #ifdef GLCD_TYPE_SSD1306_LOWMEMORY_GLCD_MODE
          GLCD_Open_PageTransaction 0,7
        #endif


            #ifndef GLCD_TYPE_SSD1306_CHARACTER_MODE_ONLY
              FilledBox 1, 10, xrange, 17
            #endif

            #ifdef GLCD_TYPE_SSD1306_LOWMEMORY_GLCD_MODE
              FilledBox 1, 10, xrange, 17
            #endif

        #ifdef GLCD_TYPE_SSD1306_LOWMEMORY_GLCD_MODE
          GLCD_Close_PageTransaction
        #endif
    Next
    wait 1 s
    GLCDCLS

   Loop


  end
