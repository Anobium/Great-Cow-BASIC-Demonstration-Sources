''' A demonstration program for GCGB and GCB.
'''
'''
''' Pulse-Width Modulation (PWM) is a scheme that provides power to a load by switching
''' quickly between fully on and fully off states. The PWM signal resembles a square wave
''' where the high portion of the signal is considered the on state and the low portion of
''' the signal is considered the off state. The high portion, also known as the pulse width,
''' can vary in time and is defined in steps. A longer, high on time will illuminate the LED
''' brighter. The frequency or period of the PWM does not change. A larger number of
''' steps applied, which lengthens the pulse width, also supplies more power to the load.
''' Lowering the number of steps applied, which shortens the pulse width, supplies less
''' power. The PWM period is defined as the duration of one complete cycle or the total
''' amount of on and off time combined.
'''
''' The demo vary the PWM to all the LEDS. This is achieved by redirected PMW6 to the LEDS.
'''
'''
'''   You need four LEDs connnect to PORTC.0, PORTC.1, PORTC.2 and PORTC.3 connected via suitable resistors.
'''  ************************************************************************************************************************************************************************************
'''
'''
'''  PIC: 16f15355
'''  Compiler: GCB
'''  IDE: GCB@SYN
'''
'''@author   EvanV
'''@licence  GPL
'''@version  1.0
'''@date     04.12.2016

  #chip 16f15355
  #Config  MCLRE_ON    'These configs = MCLR pin function enablesd, External oscillator not enabled and clockout function is disables ( enable I/O port).


  'Set the PPS of the PWM and the RS232 ports.
  'Set the PPS for this solution.
  #startup InitPPS, 85

  Sub InitPPS
    UNLOCKPPS
    RC0PPS = 0x0E      'RC0->PWM6:PWM6OUT;
    RC3PPS = 0x0E      'RC3->PWM6:PWM6OUT;
    RC1PPS = 0x0E      'RC1->PWM6:PWM6OUT;
    RC2PPS = 0x0E      'RC2->PWM6:PWM6OUT;
    LOCKPPS
  End sub

    '' -------------------LATC-----------------
    '' Bit#:  -7---6---5---4---3---2---1---0---
    '' LED:   ---------------|D5 |D4 |D3 |D2 |-
    ''-----------------------------------------
    ''
    #define LEDD2  PORTC.0
    #define LEDD3  PORTC.1
    #define LEDD4  PORTC.2
    #define LEDD5  PORTC.3
    #define LEDBANK PORTC

    'You do need to specfic the ports as outputs
    Dir     LEDD2 OUT
    Dir     LEDD3 OUT
    Dir     LEDD4 OUT
    Dir     LEDD5 OUT


    'Optimise commands  for PWM modules to reduce the size of the code produced.
    #define USE_HPWMCCP1 FALSE      ' not used Optimise
    #define USE_HPWMCCP2 FALSE      ' not used Optimise
    #define USE_HPWMCCP3 FALSE      ' not used Optimise
    #define USE_HPWMCCP4 FALSE      ' not used Optimise

    #define USE_HPWM6 TRUE          ' used so compile
    #define USE_HPWM5 FALSE         ' not used Optimise

    #define USE_HPWM_TIMER2 TRUE    ' used so compile
    #define USE_HPWM_TIMER4 FALSE   ' not used Optimise
    #define USE_HPWM_TIMER6 FALSE   ' not used Optimise

    #define myFrequency 60
    #define myTimerSelected 2
    dim myDuty as byte
        myDuty = 50 'start at 50%

    HPWM ( HPWM6, myFrequency, myDuty , myTimerSelected  )

    Do Forever
          for myDuty = 0 to 255
              wait 10 ms
              HPWM ( HPWM6, myFrequency, myDuty, myTimerSelected  )
        next
        wait 10 ms
    Loop
