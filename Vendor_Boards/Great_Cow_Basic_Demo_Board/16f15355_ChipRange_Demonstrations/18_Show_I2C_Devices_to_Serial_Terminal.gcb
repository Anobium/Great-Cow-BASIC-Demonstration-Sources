''' A demonstration program for GCGB and GCB.
'''
'''  This demonstration examines the I2C/TWI devices attached and displays then devices discovered on a serial terminal using the primary I2C module.
'''
'''   You need four LEDs connnect to PORTC.0, PORTC.1, PORTC.2 and PORTC.3 connected via suitable resistors.
'''  You will to connect I2C/TWI devices to PORTC.2 and PORTC.4 for the data and clock lines respectively.  The two lines are need to be pull high by suitable resistors supply voltage.
'''   You will also need to connect a serial Terminal to PORTC.4.  Using PUTTY.EXE for an excellent terminal program.  Also, you msy need to connect the microcontroller via a TTL converter.
'''  You will a switch connect to porta.1.  The needs to be connected the supply voltage via a 1-k pullup resistor.
'''  ************************************************************************************************************************************************************************************
'''
'''
'''  PIC: 16f15355
'''  Compiler: GCB
'''  IDE: GCB@SYN
'''
'''
'''@author   EvanV
'''@licence  GPL
'''@version  1.0
'''@date     04.12.2016


' ----- Configuration
'Chip Settings.
#CHIP 16f15355,32
#OPTION Explicit




'Set the PPS of the PWM and the RS232 ports.
'Set the PPS for this solution.
#STARTUP InitPPS, 85

Sub InitPPS
    UNLOCKPPS
    'RC5->EUSART1:RX1;
    RX1DTPPS = 0x15
    'RC4->EUSART1:TX1;K
    RC4PPS = 0x0F

    'RB1->MSSP1:SCL1;
    SSP1CLKPPS = 0x09
    'RB2->MSSP1:SDA1;
    SSP1DATPPS = 0x0A
    'RB1->MSSP1:SCL1;
    RB1PPS = 0x15
    'RB2->MSSP1:SDA1;
    RB2PPS = 0x16

    LOCKPPS
End Sub

#DEFINE SWITCH_DOWN         1
#DEFINE SWITCH_UP           0



'' -------------------LATC-----------------
'' Bit#:  -7---6---5---4---3---2---1---0---
'' LED:   ---------------|D5 |D4 |D3 |D2 |-
''-----------------------------------------
''
#DEFINE LEDD2  PORTC.0
#DEFINE LEDD3  PORTC.1
#DEFINE LEDD4  PORTC.2
#DEFINE LEDD5  PORTC.3
#DEFINE LEDBANK PORTC
#DEFINE SWITCH      PORTA.1

Dir     LEDD2 Out
Dir     LEDD3 Out
Dir     LEDD4 Out
Dir     LEDD5 Out
Dir      SWITCH In

#DEFINE USART_BAUD_RATE 19200
#DEFINE USART_TX_BLOCKING


' ----- Define Hardware settings for HWI2C
' Define I2C settings - CHANGE PORTS if required for your specific device.
#DEFINE HI2C_BAUD_RATE 400
#DEFINE HI2C_DATA PORTB.2
#DEFINE HI2C_CLOCK PORTB.1
'Initialise I2C Master
'I2C pins need to be input for SSP module
Dir HI2C_DATA In
Dir HI2C_CLOCK In
'MASTER Second Port
HI2CMode Master

' ----- Main body of program commences here.
' Now assumes Serial Terminal is operational
Dim DeviceID As Byte
Dim DISPLAYNEWLINE As Byte

HSerPrintCRLF
HSerPrint "Hardware I2C "
HSerPrintCRLF 2

Do Forever
    Set LEDD2 Off
    Set LEDD3 Off
    Set LEDD4 Off
    Set LEDD5 Off

    HSerPrint "     "
    For DeviceID = 0 To 15
        HSerPrint Hex(deviceID)
        HSerPrint " "
    Next

    For DeviceID = 0 To 255
        DisplayNewLine = DeviceID % 16
        If DisplayNewLine = 0 Then
            HSerPrintCRLF
            HSerPrint Hex(DeviceID)
            HSerPrint ": "
        End If
        HSerPrint " "
        HI2CStart
        If HI2CWaitMSSPTimeout <> TRUE Then

            HI2CSend ( deviceID )

            If HI2CAckpollState = FALSE Then
                HSerPrint   Hex(deviceID)
            Else
                HSerPrint "--"
            End If
            HI2CSend ( 0)

        End If
        HI2CStop

    Next

    HSerPrintCRLF 2
    HSerPrint   "End of Search - Press button to repeat test"
    HSerPrintCRLF 2
    Set LEDD2 On
    Set LEDD3 On
    Set LEDD4 On
    Set LEDD5 On

    Wait While switch = SWITCH_UP
Loop
End



' ----- Support methods.  Subroutines and Functions
