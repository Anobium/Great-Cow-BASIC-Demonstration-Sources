''' A demonstration program for GCGB and GCB.
'''
'''
''' Pulse-Width Modulation (PWM) is a scheme that provides power to a load by switching
''' quickly between fully on and fully off states. The PWM signal resembles a square wave
''' where the high portion of the signal is considered the on state and the low portion of
''' the signal is considered the off state. The high portion, also known as the pulse width,
''' can vary in time and is defined in steps. A longer, high on time will illuminate the LED
''' brighter. The frequency or period of the PWM does not change. A larger number of
''' steps applied, which lengthens the pulse width, also supplies more power to the load.
''' Lowering the number of steps applied, which shortens the pulse width, supplies less
''' power. The PWM period is defined as the duration of one complete cycle or the total
''' amount of on and off time combined.
'''
''' This demonstation shows a how the changing PWM values across the LEDs using the PWM module using different timers and PMW modules.
'''
'''
'''   You need four LEDs connnect to PORTD.0, PORTD.1, PORTD.2 and PORTD.3 connected via suitable resistors.
'''  ************************************************************************************************************************************************************************************
'''
'''  PIC: 16F18877
'''  Compiler: GCB
'''  IDE: GCB@SYN
'''
'''
'''@author   EvanV
'''@licence  GPL
'''@version  1.0
'''@date     04.12.2016



#CHIP 16F18877,32
#OPTION Explicit



'Set the PPS of the PWM and the RS232 ports.
'Set the PPS for this solution.
#STARTUP InitPPS, 85

Sub InitPPS
    UNLOCKPPS

    '   'RD3->PWM6:PWM6OUT;
    RD3PPS = 0x000E
    '   'RD2->PWM6:PWM6OUT;
    RD2PPS = 0x000E
    '   'RD1->PWM6:PWM6OUT;
    RD1PPS = 0x000E
    '   'RD0->PWM6:PWM6OUT;
    RD0PPS = 0x000E

'        'if you want to use PWM5 then you will need to uncomment thise PPS settings
'        RD3PPS = 0x000F;   'RD3->PWM5:PWM5OUT;
'        RD2PPS = 0x000F;   'RD2->PWM5:PWM5OUT;
'        RD1PPS = 0x000F;   'RD1->PWM5:PWM5OUT;
'        RD0PPS = 0x000F;   'RD0->PWM5:PWM5OUT;


    LOCKPPS
End Sub

#DEFINE USART_BAUD_RATE 19200
#DEFINE USART_TX_BLOCKING

'You Optimise the code... but, beware.. if you do not set a capability to TRUE... it will not be avaialbe to you!
'    #define USE_HPWM7 TRUE         ' Not avaiable on this chip
'    #define USE_HPWM6 TRUE          ' used so compile
'    #define USE_HPWM5 TRUE         ' not used Optimise
'    #define USE_HPWM_TIMER2 TRUE    ' used so compile
'    #define USE_HPWM_TIMER4 TRUE    ' not used Optimise
'    #define USE_HPWM_TIMER6 TRUE    ' not used Optimise

'' -------------------LATD-----------------
'' Bit#:  -7---6---5---4---3---2---1---0---
'' LED:   ---------------|D5 |D4 |D3 |D2 |-
''-----------------------------------------
''
#DEFINE LEDD2  PORTD.0
#DEFINE LEDD3  PORTD.1
#DEFINE LEDD4  PORTD.2
#DEFINE LEDD5  PORTD.3
#DEFINE LEDBANK portD

Dir     LEDD2 Out
Dir     LEDD3 Out
Dir     LEDD4 Out
Dir     LEDD5 Out

Dim BRIGHT As Byte


Do Forever
    For bright = 0 To 255
        Wait 10 ms
        'Four parameters
        ' 1. PWM channel - check the datasheet - you must ensure the PPS is set!!
        ' 2. frequency
        ' 3. duty
        ' 4. timer to be used - check the datasheet typically. 2, 4 or 6
        HPWM ( 6, 62, bright , 2  )
    Next
    Wait 10 ms
Loop
