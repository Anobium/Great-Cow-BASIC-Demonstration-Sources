''' Great Cow BASIC demonstration
'''
''' This demonstration simply turns the LED on using the status of the switch AND sends the status to the USB CDC serial port!
'''
'''
''' Attach an LED to the MPLAB® Xpress PIC16F15356 board that can be turned ON via a suitable resistor.
''' and OFF. The LED can be activated by driving the connected I/O line.
'''
''' Atttach a mechanical button with a 10k pull resistor. This is a generic user configurable
''' button and when a button is pressed it will drive the I/O line to GND.
''' If there is no pull-up resistor connected to the generic user button. So we enable
''' the internal pull-up in the PIC16F15356 to use the mechanical button.
'''
'''
'''@author  Evan R. Venn
'''@licence GPL
'''@version 1.00
'''@date    27/10/2018
'''********************************************************************

'Specify the Device you are compiling for - this board is the 16F15356
#CHIP 16F15356
'Great Cow BASIC simplifies the configuration by setting the frequency and the configuration for you.
'This option ensures that all variables are dimensioned in the user program.  See http://gcbasic.sourceforge.net/help/__option_explicit.html
#OPTION Explicit

'Set a constant.  The LED can be de-activated by driving the connected I/O line to the supply voltage via a suitable resistor.
#DEFINE LEDOn  = 1
'Set a constant.  The LED can be activated by driving the connected I/O line to GND.
#DEFINE LEDOff = 0

'Set a constant to refer to the LED. Attach via a suitable resistor.
#DEFINE LED0 portc.1
'Set LED as an output
Dir LED0 Out

'Set a constant to refer to the switch
#DEFINE SW0 portc.0
'Set the switch as an input
Dir SW0 In
'Enable weak pullup, see section 3.3.2 of the MPLAB-Xpress-PIC16F15356-Evaluation-Board.pdf
WPUC0 = 1

'Setup the communication EUSART settings - do not need to do much with Great Cow BASIC
#DEFINE USART_BAUD_RATE 19200
#DEFINE USART_TX_BLOCKING


'Generated by PIC PPS Tool for Great Cow Basic
'PPS Tool version: 0.0.5.22
'PinManager data: Not available (3)
'Generated for 16F15356
'
'Template comment at the start of the config file
'
#STARTUP InitPPS, 85
#DEFINE PPSToolPart 16F15356

Sub InitPPS

    'Module: EUSART1
    'TX1 > RB6
    RB6PPS = 0x000F

End Sub
'Template comment at the end of the config file


''' Start of the main Program

'A tracker to control serial output, only needed to ensure we do not constantly send data
Dim switchState As BIT
'Set the initial state
switchState = LEDOn

Wait 1 s
HSerPrintCRLF
HSerPrint "Great Cow BASIC Demonstration for the PIC16F15356"
HSerPrintCRLF

Do Forever
    'the switch has change state
    If SW0 <> switchState Then

        switchState = SW0
        If switchState = LEDOn Then
            LED0 = LEDOn
            HSerPrint "LED is On"
        Else
            LED0 = LEDOff
            HSerPrint "LED is Off"
        End If
        HSerPrintCRLF

    End If
Loop

End
