'''
'''******************************************************************
'''
'''
'''
'''  PIC: 16F18855
'''  Compiler: GCB
'''  IDE: GCB@SYN
'''
'''  Board: Xpress Evaluation Board
'''  Date: 13.3.2016
'''


' ----- Configuration
'Chip Settings.
#CHIP 16f18855,32
#OPTION Explicit


#INCLUDE <lowlevel\hwi2c2.h>


'Set the PPS of the PWM and the RS232 ports.
'Set the PPS for this solution.
#STARTUP InitPPS, 85

Sub InitPPS
    UNLOCKPPS
    'RC0->EUSART:TX;
    RC0PPS = 0x0010
    'RC1->EUSART:RX;
    RXPPS  = 0x0011

    'RC0->EUSART:TX;
    RC0PPS = 0x0010
    'RC3->MSSP2:SDA2;
    RC3PPS = 0x0017
    'RC3->MSSP2:SDA2;
    SSP2DATPPS = 0x0013
    'RC4->MSSP2:SCL2;
    SSP2CLKPPS = 0x0014
    'RC4->MSSP2:SCL2;
    RC4PPS = 0x0016
    LOCKPPS
End Sub

#DEFINE SWITCH_DOWN         0
#DEFINE SWITCH_UP           1

#DEFINE SWITCH      PORTA.5

#DEFINE LEDD2 PORTA.0
#DEFINE LEDD3 PORTA.1
#DEFINE LEDD4 PORTA.2
#DEFINE LEDD5 PORTA.3
Dir     LEDD2 Out
Dir     LEDD3 Out
Dir     LEDD4 Out
Dir     LEDD5 Out

#DEFINE ALARM1  PORTA.7
#DEFINE ALARM2  PORTA.6
Dir     ALARM1  In
Dir     ALARM2  In

#DEFINE USART_BAUD_RATE 19200
#DEFINE USART_TX_BLOCKING
#DEFINE sync SYNC_TX1STA

' ----- Define Hardware settings for HWI2C2
' Define I2C settings - CHANGE PORTS if required for your specific device.
#DEFINE HI2C2_BAUD_RATE 400
#DEFINE HI2C2_DATA PORTC.3
#DEFINE HI2C2_CLOCK PORTC.4
'Initialise I2C Master
'I2C pins need to be input for SSP2 module
Dir HI2C2_DATA In
Dir HI2C2_CLOCK In
'MASTER Second Port
HI2C2Mode Master

Dim DISPLAYNEWLINE As Byte


' ----- Main body of program commences here.
' Now assumes Serial Terminal is operational
Dim DeviceID As Byte

HSerPrintCRLF
HSerPrint "Hardware I2C2 "
HSerPrintCRLF 2

Do Forever
    Set LEDD2 Off
    Set LEDD3 Off
    Set LEDD4 Off
    Set LEDD5 Off

    HSerPrint "     "
    For DeviceID = 0 To 15
        HSerPrint Hex(deviceID)
        HSerPrint " "
    Next

    For DeviceID = 0 To 255
        DisplayNewLine = DeviceID % 16
        If DisplayNewLine = 0 Then
            HSerPrintCRLF
            HSerPrint Hex(DeviceID)
            HSerPrint ": "
        End If
        HSerPrint " "
        HI2C2Start
        If HI2C2WaitMSSPTimeout <> TRUE Then

            HI2C2Send ( deviceID )

            If HI2C2AckpollState = FALSE Then
                HSerPrint   Hex(deviceID)
            Else
                HSerPrint "--"
            End If
            HI2C2Send ( 0)


        End If
        HI2C2Stop

    Next

    HSerPrintCRLF 2
    HSerPrint   "End of Search"
    HSerPrintCRLF 2
    Set LEDD2 On
    Set LEDD3 On
    Set LEDD4 On
    Set LEDD5 On

    Wait While switch = On
Loop
End



' ----- Support methods.  Subroutines and Functions
