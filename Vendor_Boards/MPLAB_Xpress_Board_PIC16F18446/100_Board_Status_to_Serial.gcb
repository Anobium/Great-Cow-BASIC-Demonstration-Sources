''' Great Cow BASIC demonstration
'''
''' This demonstration simply turns the LED on using the status of the switch AND sends the status to the USB CDC serial port!
'''
''' There is one yellow LED available on the MPLAB® Xpress PIC16F18446 board that can be turned ON
''' and OFF. The LED can be activated by driving the connected I/O line to GND.
'''
''' MPLAB® Xpress PIC16F18446 contains one mechanical button. This is a generic user configurable
''' button and when a button is pressed it will drive the I/O line to GND.
''' There is no pull-up resistor connected to the generic user button. So we enable
''' the internal pull-up in the PIC16F18446 to use the mechanical button.
'''
'''@author  Evan R. Venn
'''@licence GPL
'''@version 1.00
'''@date    30/06/2018
'''********************************************************************

'Specify the Device you are compiling for - this board is the 16F18446
#CHIP 16F18446
'Great Cow BASIC simplifies the configuration by setting the frequency and the configuration for you.
'This option ensures that all variables are dimensioned in the user program.  See http://gcbasic.sourceforge.net/help/__option_explicit.html
#OPTION Explicit

'Set a constant.  The LED can be activated by driving the connected I/O line to GND, see section 3.3.2 of the MPLAB-Xpress-PIC16F18446-Evaluation-Board.pdf
#DEFINE LEDOn  = 0
'Set a constant.  The LED can be de-activated by driving the connected I/O line to the supply voltage, see section 3.3.2 of the MPLAB-Xpress-PIC16F18446-Evaluation-Board.pdf
#DEFINE LEDOff = 1

'Set a constant to refer to the LED.  RA2 is a Yellow LED called LED0 Edge connector, see section 3.3.1 of the MPLAB-Xpress-PIC16F18446-Evaluation-Board.pdf
#DEFINE LED0 porta.2
'Set LED as an output
Dir LED0 Out


'Set a constant to refer to the switch
#DEFINE SW0 portc.2
'Set the switch as an input
Dir SW0 In
'Enable weak pullup, see section 3.3.2 of the MPLAB-Xpress-PIC16F18446-Evaluation-Board.pdf
WPUC2 = 1

'Setup the communication EUSART settings - do not need to do much with Great Cow BASIC
#DEFINE USART_BAUD_RATE 19200
#DEFINE USART_TX_BLOCKING


'Generated by PIC PPS Tool for Great Cow Basic
'PPS Tool version: 0.0.5.19
'PinManager data: v1.65.2
'
'Template comment at the start of the config file
'
#STARTUP InitPPS, 85

Sub InitPPS

    'Module: EUSART1
    'RB6 > RX1
    RX1DTPPS = 0x000E
    'TX1 > RB4
    RB4PPS = 0x000F

End Sub
'Template comment at the end of the config file



''' Start of the main Program

'A tracker to control serial output, only needed to ensure we do not constantly send data
Dim switchState As BIT
'Set the initial state
switchState = LEDOn

Wait 1 s
HSerPrintCRLF
HSerPrint "Great Cow BASIC Demonstration for the MPLAB® Xpress PIC16F18446 Evaluation Board"
HSerPrintCRLF

Do Forever
    'the switch has change state
    If SW0 <> switchState Then

        switchState = SW0
        If switchState = LEDOn Then
            LED0 = LEDOn
            HSerPrint "LED is On"
        Else
            LED0 = LEDOff
            HSerPrint "LED is Off"
        End If
        HSerPrintCRLF

    End If
Loop

End
