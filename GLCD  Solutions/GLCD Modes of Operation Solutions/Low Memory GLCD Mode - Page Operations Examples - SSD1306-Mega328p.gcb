'''A program  for GCGB and GCB.
'''--------------------------------------------------------------------------------------------------------------------------------
'''This program demonstrations the options supported by GLCD modes.
'''
'''    #define GLCD_TYPE_SSD1306_CHARACTER_MODE_ONLY
'''    #define GLCD_TYPE_SSD1306_LOWMEMORY_GLCD_MODE

'''  Operational Options
'''
'''  Full GLCD mode - Hi memory device         | No defines required
'''  Low GLCD memory mode - Lo memory device   | GLCD_TYPE_SSD1306_LOWMEMORY_GLCD_MODE ( as GLCD_TYPE_SSD1306_CHARACTER_MODE_ONLY will be set automatically)
'''  Low GLCD memory mode - Hi memory device   | GLCD_TYPE_SSD1306_LOWMEMORY_GLCD_MODE and GLCD_TYPE_SSD1306_CHARACTER_MODE_ONLY to force mode of operation
'''                                              Note: GLCD_TYPE_SSD1306_LOWMEMORY_GLCD_MODE alone WILL NOT operate as expected.  GLCD_TYPE_SSD1306_CHARACTER_MODE_ONLY must be added to user code
'''  Character mode - Lo or Hi memory device   | GLCD_TYPE_SSD1306_CHARACTER_MODE_ONLY

''' Character memory mode - screen addressing
'''     8 lines of text, any GLCDPrint (xpos, YPOS, text) and YPOS will be translated to Address Line #n.
'''     Address Line #0 = YPOS 0 to 7
'''     Address Line #1 = YPOS 8 to 15
'''     Address Line #2 = YPOS 16 to 23
'''     Address Line #3 = YPOS 14 to 31
'''     Address Line #4 = YPOS 32 to 39
'''     Address Line #5 = YPOS 40 to 47
'''     Address Line #6 = YPOS 48 to 55
'''     Address Line #7 = YPOS 56 to 63
'''
'''
'''
''' Low GLCD memory mode
'''
'''   GLCD_Open_PageTransaction and GLCD_Close_PageTransaction added to support Low memory configuration
'''   Any commands between GLCD_Open_PageTransaction and GLCD_Close_PageTransaction should be GLCD focused only.
'''   Any commands within the transaction WILL BE called 8 (EIGHT) times. So, if you increment variables... they will be incremented 8 times
'''   Any commands between GLCD_Open_PageTransaction and GLCD_Close_PageTransaction should be GLCD focused only.
'''   Methods such as scroll, pixelstatus WILL NOT work in low memory mode.
'''   Print characters first then graphics.
'''
'''   Example of low memory usage
'''    #define GLCD_TYPE_SSD1306_CHARACTER_MODE_ONLY    '''This defined will be required on high RAM devices to activate low memory capability
'''    #define GLCD_TYPE_SSD1306_LOWMEMORY_GLCD_MODE
'''
'''      GLCD_Open_PageTransaction 0,7
'''        GLCDPrint 0,10, "Great Cow BASIC"
'''        GLCDPrint 0,52, "Page 2"
'''        Box 0,0,127,63
'''      GLCD_Close_PageTransaction
'''
'''
'''@author     EvanV
'''@licence    GPL
'''@version    1.00
'''@date       21.08.17
'''********************************************************************************


  #chip mega328p,16
  #option explicit
  #include <UNO_mega328p.h>
  #include <glcd.h>

  ; ----- Define GLCD Hardware settings
  #define GLCD_TYPE GLCD_TYPE_SSD1306
  #define GLCD_I2C_Address 0x78
  #define GLCD_PROTECTOVERRUN

  ' Define Hardware I2C settings
  #Define HI2C_BAUD_RATE 400
  #Define HI2C_DATA
  HI2CMode Master

'    #define GLCD_TYPE_SSD1306_CHARACTER_MODE_ONLY    '''This defined will be required on high RAM devices to activate low memory capability
'  #define GLCD_TYPE_SSD1306_LOWMEMORY_GLCD_MODE


  GLCDCLS

  dim Mxpos as Byte
  dim Mxstring as string * 11


  GLCD_Open_PageTransaction 0,7
      GLCDPrint 1, 0, "Great Cow BASIC"
      GLCDPrint 1, 8, "Meter"
      Line 0, 0, 127, 63
      Line 127, 0, 0, 63
  GLCD_Close_PageTransaction

  end

  do Forever

    Mxstring = ""
    for Mxpos = 0 to 11

      Mxstring = pad("", Mxpos, "|")
      Mxstring = pad(Mxstring, 11-Mxpos, " ")
      GLCD_Open_PageTransaction 0,7

          box 0, 0, 127, 63
          box 0, 0, 35, 50
          box 0, 50, 127, 50
          GLCDPrint 1, 1, "Meter"
          line 60, 25, 120, 25
          GLCDPrint 52, 30, "0v"
          GLCDPrint 112, 30, "5v"
          GLCDPrint 57, 15, Mxstring

      GLCD_Close_PageTransaction
      wait 100 ms

    next

    for Mxpos = 0 to 13

      GLCD_Open_PageTransaction 7,7

          Line 1, 56, 126, 56, GLCDBackground
          Line 10*Mxpos, 56, (10*Mxpos)+5, 56
          box 0, 0, 127, 63

      GLCD_Close_PageTransaction
      wait 100 ms

    next

  Loop
