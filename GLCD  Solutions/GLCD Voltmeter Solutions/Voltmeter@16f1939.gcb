'''A demonstration program for GCGB and GCB.
'''--------------------------------------------------------------------------------------------------------------------------------
'''This program demonstrates the basic primatives a voltmeter on a Graphical LCD (GLCD)
''':
'''You can change the microcontroller, the GLCD and the analogue port.
'''
'''To change the microcontroller simply change the #chip statement
'''To change the GLCD replace GLCD definition
'''To change the analogue port change the ADCChannelSelected constant
'''
''':
'''@author  StanC
'''@licence GPL
'''@version 1.01a
'''@date    14.07.2017
'''********************************************************************************



;analogue 5V voltmeter
#chip 16f1939,32
    #option explicit
    #INCLUDE <TRIG2PLACES.H>
    ;
     #include <glcd.h>


'GLCD selection
  #define GLCD_TYPE GLCD_TYPE_KS0108               ' This is the Default value, not really required.
' #define GLCDDirection 1                          ' Invert display, not always required

         #define GLCD_CS1 PORTa.7    'D12 to actually since CS1, CS2 can be reversed on some devices
         #define GLCD_CS2 PORTa.6
         #define GLCD_DB0 PORTD.0 'D0 to pin 7 on LCD
         #define GLCD_DB1 PORTD.1 'D1 to pin 8 on LCD
         #define GLCD_DB2 PORTD.2 'D2 to pin 9 on LCD
         #define GLCD_DB3 PORTD.3 'D3 to pin 10 on LCD
         #define GLCD_DB4 PORTD.4 'D4 to pin 11 on LCD
         #define GLCD_DB5 PORTD.5 'D5 to pin 12 on LCD
         #define GLCD_DB6 PORTD.6 'D6 to pin 13 on LCD
         #define GLCD_DB7 PORTD.7 'D7 to pin 14 on LCD

         #define GLCD_RS PORTa.0
         #define GLCD_Enable PORTA.1
         #define GLCD_RW PORTA.2
         #define GLCD_RESET PORTA.3
'

        #define KS0108ReadDelay   3     ; 2 normal usage, 3 for 32 mhz!
'       #define KS0108WriteDelay  1     ; 1 normal usage, 0 works
'       #define KS0108ClockDelay  1     ; 1 normal usage, 0 works


'analogue port selection
        #define ADCChannelSelected AN5
        #define ADSpeed LowSpeed

dim radius,xcentre,ycentre,xend,old_xend,yend,old_yend as byte
dim angle,old_angle,adcval as word
dim volts as string * 4
xcentre=63
ycentre=58
;draw dial
GLCDCLS
box 0,0,127,63
for radius=144 to 141
  dial
next radius
GLCDDrawChar (5,40,asc("0")):GLCDDrawChar (115,40,asc("5"))
radius=120
angle=1
anglecheck
;main
do
  old_angle=angle
;--- DVM
  adcval=ReadAD(ADCChannelSelected)
  angle=adcval/2
;
  adcval=adcval*100
  adcval=adcval/51
  volts=str(adcval)
  if adcval<10 then
    volts="0.0"+left(volts,1,1)
    goto volt_format_done
  end if
  if adcval<100 then
    volts="0."+mid(volts,1,2)
  else
    volts=left(volts,1,1)+"."+mid(volts,2,2)
  end if
  volt_format_done:
;---
  anglecheck
  old_xend = xcentre + radius  * sin (old_angle)/255
  old_yend = ycentre - radius  * cos (old_angle)/255
  xend = xcentre + radius  * sin (angle)/255
  yend = ycentre - radius  * cos (angle)/255
  if old_angle<>angle then ;-- update screen if volts has changed
;   erase old needle
    line (xcentre,ycentre,old_xend,old_yend,0)
;   draw V
    line (55,22,63,38):line (63,38,71,22)
;   print new voltage
    GLCDPrint (50,10,volts,1) ;print Voltage
;   draw new needle
    line (xcentre,ycentre,xend,yend,1)
    wait 20 ms
  end if
loop
;end main
sub plotdial
  xend = 63 + radius  * sin (angle)/255
  yend = 58 - radius  * cos (angle)/255
  pset (xend,yend,1)
end sub
;
sub dial
  for angle= 296 to 359
    plotdial
  next angle
;
  for angle= 0 to 63
    plotdial
  next angle
end sub
;
sub anglecheck
  if angle>127 then
    angle =angle-127
  else
    angle =296+angle
  end if
end sub