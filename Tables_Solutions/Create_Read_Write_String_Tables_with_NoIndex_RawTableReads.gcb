'''A program  for GCGB and GCB.
'''--------------------------------------------------------------------------------------------------------------------------------
'''This program shows how to create and use table strings.
'''
'''The example menu system is shown on a serial terminal.
'''
'''The method to show the menu strings simply 'walks' through the table of string looking for the terminator \0.
'''If the _index variable matches the StrIndex variables then the correct table string is returned.
'''The interesting insight into this method...the greater the value of _index the more time it will take to return the table string.
'''
'''
'''@author     EvanV
'''@licence    GPL
'''@version    1.00
'''@date       19/03/2021
'''********************************************************************************

; ----- Configuration
#chip 16f88


#define USART_BAUD_RATE 9600
#define USART_TX_BLOCKING '< wait for tx done
#Define TblStrNum 20      '< num of strings

#option explicit


Dim Result as String
Dim Result0 as String     '< for bug in older compiler <= V0.098.00
Dim StrNum as Byte        '< num of string in table
Dim StrIndex as Word      '< String to get
Dim _Index as Byte
Dim Lp1 as Word           '< loops
Dim Item as Byte          '< byte from table
Dim StrStart as Word
Dim StrEnd as Word





    ShowMenuItems
End
'---------------------------


Sub ShowMenuItems

          For _Index = 1 to TblStrNum
            ReadTableMenu ( _Index )
            HSerPrint leftpad( str(_Index ), 2 ," ") +  ")"
            HSerPrint Result
            HserPrintCRLF
          Next
          HserPrintCRLF
          HserPrint "----------------"
          HserPrintCRLF
'          Wait 5 ms   '<Comment for sim
End Sub


Sub ReadTableMenu ( _Index )
          Result = ""

          Dim TableSize as word
          ReadTable MnuTxt1, 0, TableSize
          StrIndex = 0
          StrStart = 1

          For Lp1 = 1 to  TableSize
              ReadTable MnuTxt1, Lp1, StrEnd

              If StrEnd = 0 then
                  StrIndex =  StrIndex + 1
                  if StrIndex = _Index then
                      StrEnd = Lp1
                      Exit For
                  else
                      Lp1++
                      StrStart = Lp1
                  End if
              End if

          Next

          If StrEnd = 0 then return

          For Lp1 = StrStart to StrEnd - 1
             ReadTable MnuTxt1, Lp1, Item  '< Readtable data item
             Result0 = Result + Chr(Item)
             Result = Result0
          Next

End Sub

    '==========  TABLES ===============

    Table  MnuTxt1

       "\"Menu Item 1\"\0", "Menu Item 2\0", "Menu Item 3\0", "Menu Item 4\0", "Menu Item 5\0"  ' SUB1
       "Config1 Itm 1\0", "Config1 Itm 2\0", "Config1 Itm 3\0", "Config1 Itm 4\0"               ' SUB2
       "DataLog Itm 1\0", "DataLog Itm 2\0", "DataLog Itm 3\0"                                  ' SUB3
       "Diag Itm 1\0", "Diag Itm 2\0", "Diag Itm 3\0", "Diag Itm 4\0"                           ' SUB4
       "Help Itm 1\0", "Help Itm 2\0", "Help Itm 3\0", "Help Itm 4\0"                           ' SUB5

    End Table
