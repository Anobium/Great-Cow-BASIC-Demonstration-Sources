' Use program uses the Great Cow BASIC 10-bit PWM. This chip has a 10-bit PWM module.
' This program will setup the specified PWM Channel using the in-build Great Cow BASIC PWM calculations
' The registers and bits used can setup any 10-bit PWM module.
'
' This example uses PWM3 with timer 2.
'
' Fixed frequency of 76mhz with 50% duty
'
#chip 16f1705,32


     'Generated by PIC PPS Tool for Great Cow Basic
    'PPS Tool version: 0.0.5.5
    'PinManager data: 07/03/2017
    '
    'Template comment at the start of the config file
    '
    #startup InitPPS, 85

    Sub InitPPS

            'Module: PWM3
            RC5PPS = 0x000E    'PWM3OUT > RC5

    End Sub
    'Template comment at the end of the config file


    'Set the PWM pin to output mode
    DIR PORTC.5 out

    'Main code
    'freq 76khz
    'static duty from 50
    'hpwm3 using timer 2

    #define PWM_Freq 76
    #define PWM_Duty 50

    'The pwm channel MUST match the PPS!!!!!!

    'The PWM duty parameters - you will need to change the pXsel0 and pXsel0 (change the X), change PWMXEN (change the X) and select the correct bit value for pXsel0 and pXsel0 from the datasheet
    pwm3dch = DutyCycleH    'this is a system wide constants based on the PWM_Duty constant
    pwm3dcl = DutyCycleL    'this is a system wide constants based on the PWM_Duty constant

    'The PWM timer select parameters - you will need to change the pXsel0 and pXsel0 (change the X), change PWMXEN (change the X) and select the correct bit value for pXsel0 and pXsel0 from the datasheet
    p3tsel0 = 0             'select timer 2 for PWM3  - see the datasheet
    p3tsel1 = 0             'select timer 2 for PWM3  - see the datasheet

    'The PWM enable parameter
    PWM3EN = 1              'finally, enable PWM3

    'The timer parameters - you will need to change pr2 to pr2 | pr4 | pr6 then start the correct timer
    pr2 = PRxPeriodRegister 'PRxPeriodRegister is a system wide constants based on the PWM_Freq constant. Change the prX to pr2|pr4|pr6



    ' if your desire PWM_FREQ is less than the microcontroller clock speed then you NEED this section
    ' if TimerXPrescaleSelectValue is not equal to 1 then you need this code to set the Prescaler
    if TimerXprescaleselectValue = 1 then
          'change TxCKPS0 and TxCKPS1 to match your desired timer
          SET T2CKPS0 OFF
          SET T2CKPS1 OFF

    else if TimerXprescaleselectValue = 4 then
          'change TxCKPS0 and TxCKPS1 to match your desired timer
          SET T2CKPS0 ON
          SET T2CKPS1 OFF

    else if TimerXprescaleselectValue = 16 then
          'change TxCKPS0 and TxCKPS1 to match your desired timer
          SET T2CKPS0 OFF
          SET T2CKPS1 ON

    end if

    StartTimer 2            'start timer 2

    do
        'maintain the processor....
    Loop
